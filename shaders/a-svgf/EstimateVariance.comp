#version 450
#extension GL_GOOGLE_include_directive : require

/*
Adapted from https://cg.ivd.kit.edu/atf.php
Modifications Copyright 2021/2022 Oskar Homburg

Copyright (c) 2018, Christoph Schied
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of the Karlsruhe Institute of Technology nor the
      names of its contributors may be used to endorse or promote products
      derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY
DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

#include "colorspace.glsl"
#include "svgf_shared.glsl"

void main()
{
    ivec2 ipos = ivec2(gl_GlobalInvocationID.xy);
    vec2 m = imageLoad(img_moments, ipos).rg;

    float histlen = imageLoad(img_histlen, ipos).r;

    vec4 c = imageLoad(img_accumulated, ipos);

    const float hist_len_thresh = 4.0;

    float z_center = abs(imageLoad(tex_vbuf_curr, ipos).x);
    float delta_z = abs(z_center - imageLoad(tex_vbuf_curr, ipos + ivec2(1, 0)).x);
    if (z_center < 0) {
        imageStore(img_varianceA, ipos, vec4(c.rgb, 0));
        return;
    }

    float dv_center = imageLoad(tex_volume_curr, ipos).x;

    uint mesh_id_center = floatBitsToUint(imageLoad(tex_vbuf_curr, ipos).y);
    if (histlen < hist_len_thresh && mesh_id_center != 0)
    {
        {
            float l = luminance(c.rgb);
            m += vec2(l, l * l);
        }

        vec3 n_center = uncompress_normal(imageLoad(tex_normal_curr, ipos).rg);

        float sum_w = 1.0;
        int r = histlen > 1 ? 2 : 3;
        for(int yy = -r; yy <= r; yy++) {
            for(int xx = -r; xx <= r; xx++) {
                if(xx != 0 || yy != 0) {
                    ivec2 p = ipos + ivec2(xx, yy);
                    vec4 c_p = imageLoad(img_accumulated, p);

                    float l = luminance(c_p.rgb);

                    float z_p = abs(imageLoad(tex_vbuf_curr, p).x);
                    vec3 n_p = uncompress_normal(imageLoad(tex_normal_curr, p).rg);

                    float w_z = abs(z_p - z_center) / (delta_z * length(vec2(xx, yy)) + 1e-2);
                    float w_n = pow(max(0, dot(n_p, n_center)), 128.0);

                    uint mesh_id_p = floatBitsToUint(imageLoad(tex_vbuf_curr, p).y);

                    float dv_p = imageLoad(tex_volume_curr, p).x;
                    float w_v = exp(-abs(dv_p - dv_center));

                    float w = w_v * exp(- w_z) * w_n * (mesh_id_center == mesh_id_p ? 1.0 : 0.0);

                    if(isnan(w))
                        w = 0.0;

                    sum_w += w;

                    m += vec2(l, l * l) * w;
                    c.rgb += c_p.rgb * w;
                }
            }
        }

        m /= sum_w;
        c.rgb /= sum_w;

        imageStore(img_varianceA, ipos, vec4(c.rgb, (1.0 + 3.0 * (1.0 - histlen / hist_len_thresh)) * max(0.0, m.y - m.x * m.x)));
    }
    else {
        float variance_temporal = max(0.0, m.y - m.x * m.x);
        imageStore(img_varianceA, ipos, vec4(c.rgb, variance_temporal));
    }
}
