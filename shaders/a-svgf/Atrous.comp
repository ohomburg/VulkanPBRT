#version 450
#extension GL_GOOGLE_include_directive : require

/*
Adapted from https://cg.ivd.kit.edu/atf.php
Modifications Copyright 2021/2022 Oskar Homburg

Copyright (c) 2018, Christoph Schied
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of the Karlsruhe Institute of Technology nor the
      names of its contributors may be used to endorse or promote products
      derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY
DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

#include "colorspace.glsl"

#include "layout.glsl"

const float gaussian_kernel[3][3] = {
	{ 1.0 / 16.0, 1.0 / 8.0, 1.0 / 16.0 },
	{ 1.0 / 8.0,  1.0 / 4.0, 1.0 / 8.0  },
	{ 1.0 / 16.0, 1.0 / 8.0, 1.0 / 16.0 }
};

float compute_sigma_luminance(float center, ivec2 ipos)
{
	const int r = 1;

	float sum = center * gaussian_kernel[0][0];

	for(int yy = -r; yy <= r; yy++) {
		for(int xx = -r; xx <= r; xx++) {
			if(xx != 0 || yy != 0) {
				ivec2 p = ipos + ivec2(xx, yy);
				float v = imageLoad(img_varianceA, p).a;
				float w = gaussian_kernel[xx + 1][yy + 1];
				sum += v * w;
			}
		}
	}

	return sqrt(max(sum, 0.0));
}

ivec2 ipos = ivec2(gl_GlobalInvocationID.xy);

vec4  color_center  = imageLoad(img_varianceA,  ipos);
vec3  normal_center = imageLoad(tex_normal_curr, ipos).rgb;
float z_center      = abs(imageLoad(tex_vbuf_curr, ipos).x);
float z_grad        = abs(abs(imageLoad(tex_vbuf_curr, ipos + ivec2(1, 1)).x) - z_center);

float l_center      = luminance(color_center.rgb);
float sigma_l       = compute_sigma_luminance(color_center.a, ipos) * 3.0;

vec3  sum_color       = color_center.rgb;
float sum_variance    = color_center.a;
float sum_weight      = 1.0;

void tap(ivec2 offset, float kernel_weight)
{
	ivec2 p = ipos + offset;

	vec4  color_p     = imageLoad(img_varianceA, p);
	vec3  normal_p    = imageLoad(tex_normal_curr, p).rgb;
	float z_p         = abs(imageLoad(tex_vbuf_curr, p).x);
	float l_p         = luminance(color_p.rgb);

	float w_l = abs(l_p - l_center) / (sigma_l + 1e-10); 
	float w_z = 3.0 * abs(z_p - z_center) / (z_grad * length(vec2(offset) * step_size) + 1e-2);
	float w_n = pow(max(0, dot(normal_p, normal_center)), 128.0);

	float w = exp(-w_l * w_l - w_z) * kernel_weight * w_n;

	sum_color    += color_p.rgb * w;
	sum_variance += w * w * color_p.a;
	sum_weight   += w;
}

void subsampled()
{
	/*
	| | |x| | |
	| |x| |x| |
	|x| |x| |x|
	| |x| |x| |
	| | |x| | |
	*/

	if((iteration & 1) == 0)
	{
		/*
		| | | | | |
		| |x| |x| |
		|x| |x| |x|
		| |x| |x| |
		| | | | | |
		*/
		tap(ivec2(-2,  0) * step_size, 1.0);
		tap(ivec2( 2,  0) * step_size, 1.0);
	}
	else
	{
		/*
		| | |x| | |
		| |x| |x| |
		| | |x| | |
		| |x| |x| |
		| | |x| | |
		*/
		tap(ivec2( 0, -2) * step_size, 1.0);
		tap(ivec2( 0,  2) * step_size, 1.0);
	}

	tap(ivec2(-1,  1) * step_size, 1.0);
	tap(ivec2( 1,  1) * step_size, 1.0);

	tap(ivec2(-1, -1) * step_size, 1.0);
	tap(ivec2( 1, -1) * step_size, 1.0);
}

void box(int r)
{
	for(int yy = -r; yy <= r; yy++) {
		for(int xx = -r; xx <= r; xx++) {
			if(xx != 0 || yy != 0) {
				tap(ivec2(xx, yy) * step_size, 1.0);
			}
		}
	}
}

void box3() { box(1); }
void box5() { box(2); }

void atrous()
{
	const float kernel[3] = { 1.0, 2.0 / 3.0, 1.0 / 6.0 };

	tap(ivec2( 1,  0) * step_size, 2.0 / 3.0);
	tap(ivec2( 0,  1) * step_size, 2.0 / 3.0);
	tap(ivec2(-1,  0) * step_size, 2.0 / 3.0);
	tap(ivec2( 0, -1) * step_size, 2.0 / 3.0);

	tap(ivec2( 2,  0) * step_size, 1.0 / 6.0);
	tap(ivec2( 0,  2) * step_size, 1.0 / 6.0);
	tap(ivec2(-2,  0) * step_size, 1.0 / 6.0);
	tap(ivec2( 0, -2) * step_size, 1.0 / 6.0);

	tap(ivec2( 1,  1) * step_size, 4.0 / 9.0);
	tap(ivec2(-1,  1) * step_size, 4.0 / 9.0);
	tap(ivec2(-1, -1) * step_size, 4.0 / 9.0);
	tap(ivec2( 1, -1) * step_size, 4.0 / 9.0);

	tap(ivec2( 1,  2) * step_size, 1.0 / 9.0);
	tap(ivec2(-1,  2) * step_size, 1.0 / 9.0);
	tap(ivec2(-1, -2) * step_size, 1.0 / 9.0);
	tap(ivec2( 1, -2) * step_size, 1.0 / 9.0);

	tap(ivec2( 2,  1) * step_size, 1.0 / 9.0);
	tap(ivec2(-2,  1) * step_size, 1.0 / 9.0);
	tap(ivec2(-2, -1) * step_size, 1.0 / 9.0);
	tap(ivec2( 2, -1) * step_size, 1.0 / 9.0);

	tap(ivec2( 2,  2) * step_size, 1.0 / 36.0);
	tap(ivec2(-2,  2) * step_size, 1.0 / 36.0);
	tap(ivec2(-2, -2) * step_size, 1.0 / 36.0);
	tap(ivec2( 2, -2) * step_size, 1.0 / 36.0);
}

void main()
{
	if(z_center.x > 0) {
	    // only filter foreground pixels
        switch(FILTER_KERNEL)
        {
        case 0: atrous(); break;
        case 1: box3(); break;
        case 2: box5(); break;
        case 3: subsampled(); break;
        case 4:
            if(step_size == 1)
                box3();
            else
                subsampled();
            break;
        case 5:
            if(step_size == 1)
                box5();
            else
                subsampled();
            break;
        }
	}

	sum_color    /= sum_weight;
	sum_variance /= sum_weight * sum_weight;

	vec4 frag_color      = vec4(sum_color, sum_variance);

	if(modulate_albedo > 0) {
		frag_color.rgb *= imageLoad(tex_albedo, ipos).rgb;
	}

	imageStore(img_varianceB, ipos, frag_color);
}
